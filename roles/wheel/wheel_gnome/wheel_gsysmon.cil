;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Macros
;

(in wheel_gsysmon
	(macro auto_subj_type_transition_gsysmon_helper ((type ARG1))
		(call gsysmon_helper.spec_auto_subj_type_transition (ARG1
			subj))))

;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Policy
;

(block wheel_gsysmon
	(blockinherit gsysmon.subj_blk)

	(call wheel_gnomeapp.spec_common_gnomeapplication (subj
		gsysmon.tmpfs_user_file))

	(call fs.getattr_all_fs (subj))

	(call subj.getattr_common_subj (subj))
	(call subj.read_state_common_subj (subj))
	(call subj.setsched_common_subj (subj))
	(call subj.send_sigkill_common_subj (subj))
	(call subj.send_signal_common_subj (subj))
	(call subj.send_sigstop_common_subj (subj))

	(call wheel_gvfsdud2.sessbus_chat_client_subj_type (subj))
	(call wheel_gvfsdgoavm.sessbus_chat_client_subj_type (subj))

	(tunableif enable_rbacsep
		(true
			(call rbacsep.exempt_subj_type (subj))))

	(optional wheel_gsysmon_optional_wheel_polkit_exec
		(call wheel_polkit_exec.auto_subj_type_transition (subj)))

	(optional wheel_gsysmon_optional_wheel_sessbus
		(call wheel_sessbus.service_subj_type (subj))))
