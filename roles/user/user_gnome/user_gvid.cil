;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Macros
;

(in user_gvid
	(macro sessbus_chat_client_subj_type ((type ARG1))
		(typeattributeset sessbus_chat_client_subj_type_attribute
			ARG1)))

;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Policy
;

(block user_gvid
	(typeattribute sessbus_chat_client_subj_type_attribute)

	(blockinherit gvid.subj_blk)

	(call user_gnomeapp.spec_common_gnomeapplication (subj
		gvid.tmpfs_user_file))
	(call user_gnomeapp.file_chooser_dialog_subj_type (subj))

	(call user_gvidap.auto_subj_type_transition (subj))
	(call user_gvidvt.auto_subj_type_transition (subj))

	(call user_gvfsdmd.client_subj_type (subj))

	(optional user_gvid_optional_user_dleyna
		(call user_dleyna.sessbus_chat_client_subj_type (subj)))

	(optional user_gvid_optional_user_gs
		(call user_gs.sessbus_chat_client_subj_type (subj)))

	(optional user_gvid_optional_user_gsd
		(call user_gsd.sessbus_chat_client_subj_type (subj)))

	(optional user_gvid_optional_user_gstreamer
		(call user_gstreamer.client_subj_type (subj)))

	(optional user_gvid_optional_user_pa
		(call user_pa.auto_subj_type_transition (subj)))

	(optional user_gvid_optional_user_sessbus
		(call user_sessbus.daemon (subj gvid.cmd_file)))

	(optional user_gvid_optional_user_tracker
		(call user_tracker.client_subj_type (subj)))

	(optional
		user_gvid_sessbus_chat_client_subj_type_optional_user_sessbus
		(allow sessbus_chat_client_subj_type_attribute subj (dbus
			(send_msg)))
		(allow subj sessbus_chat_client_subj_type_attribute (dbus
			(send_msg)))))

(block user_gvidap
	(blockinherit gvidap.subj_blk))

(block user_gvidvt
	(blockinherit gvidvt.subj_blk)

	(call user_gvfsd.sessbus_chat_client_subj_type (subj))
	(call user_gvfsdrecent.sessbus_chat_client_subj_type (subj))

	(call user_gvfsdsftp.sessbus_chat_client_subj_type (subj))
	(call user_gvfsdsftp.rw_inherited_unix_stream_sockets (subj))

	(optional user_gvidvt_optional_user_gstreamer
		(call user_gstreamer.client_subj_type (subj)))

	(optional user_gvidvt_optional_user_sessbus
		(call user_sessbus.client_subj_type (subj))))
