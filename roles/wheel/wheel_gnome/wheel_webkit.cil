;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Macros
;

(in wheel_webkitpp
	(macro sessbus_chat_client_subj_type ((type ARG1))
		(typeattributeset sessbus_chat_client_subj_type_attribute
			ARG1)))

;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Policy
;

(block wheel_webkitwp
	(blockinherit webkitwp.subj_blk)

	(call wheel_gnomeapp.spec_common_gnomeapplication (subj
		webkitwp.tmpfs_user_file))

	(call wheel_gvfsdmd.client_subj_type (subj))

	(optional wheel_webkitwp_optional_wheel_gcs
		(call wheel_gcs.rw_unix_stream_sockets (subj))
		(call wheel_gcs.sessbus_chat_client_subj_type (subj)))

	(optional wheel_webkitwp_optional_wheel_gitg
		(call wheel_gitg.rw_unix_stream_sockets (subj)))

	(optional wheel_webkitwp_optional_wheel_goais
		(call wheel_goais.sessbus_chat_client_subj_type (subj)))

	(optional wheel_webkitwp_optional_wheel_gstreamer
		(call wheel_gstreamer.client_subj_type (subj)))

	(optional wheel_webkitwp_optional_wheel_gcal
		(call wheel_gcal.use_fd (subj)))

	(optional wheel_webkitwp_optional_wheel_gdocs
		(call wheel_gdocs.rw_unix_stream_sockets (subj))))

(block wheel_webkitnp
	(blockinherit webkitnp.subj_blk)

	(optional wheel_webkitnp_subj_type_optional_wheel_sessbus
		(call wheel_sessbus.client_subj_type (subj))))

(block wheel_webkitpp
	(typeattribute sessbus_chat_client_subj_type_attribute)

	(blockinherit webkitpp.subj_blk)

	(call wheel_atspi.tmpfs_client (subj webkitpp.tmpfs_user_file))

	(call wheel_gvfsd.sessbus_chat_client_subj_type (subj))

	(optional
		wheel_webkitpp_sessbus_chat_client_subj_type_optional_wheel_sessbus
		(allow sessbus_chat_client_subj_type_attribute subj (dbus
			(send_msg)))
		(allow subj sessbus_chat_client_subj_type_attribute (dbus
			(send_msg)))))
