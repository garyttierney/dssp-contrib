;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Macros
;

(in user_vino
	(macro sessbus_chat_client_subj_type ((type ARG1))
		(typeattributeset sessbus_chat_client_subj_type_attribute
			ARG1)))

;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Policy
;

(block user_vino
	(typeattribute sessbus_chat_client_subj_type_attribute)

	(blockinherit vino.subj_blk)

	(call user_atspi.tmpfs_client (subj vino.tmpfs_user_file))

	(call user_dconf.client_subj_type (subj))

	(optional user_vino_optional_user_atspi_sessbus_launch
		(call user_atspi_sessbus_launch.sessbus_chat_client_subj_type
			(subj)))

	(optional user_vino_optional_user_gkrd
		(call user_gkrd.sessbus_chat_client_subj_type (subj)))

	(optional user_vino_optional_user_gs
		(call user_gs.forked_subj_type (subj)))

	(optional user_vino_optional_user_gsd
		(call user_gsd.sessbus_chat_client_subj_type (subj)))

	(optional user_vino_optional_user_gvfsd
		(call user_gvfsd.sessbus_chat_client_subj_type (subj)))

	(optional user_vino_optional_user_sessbus
		(call user_sessbus.daemon (subj vino.cmd_file)))

	(optional user_vino_sessbus_chat_client_subj_type_optional_user_sessbus
		(allow sessbus_chat_client_subj_type_attribute subj (dbus
			(send_msg)))
		(allow subj sessbus_chat_client_subj_type_attribute (dbus
			(send_msg)))))
