;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Macros
;

(in sysadm_gom
	(macro sessbus_chat_client_subj_type ((type ARG1))
		(typeattributeset sessbus_chat_client_subj_type_attribute
			ARG1)))

;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Policy
;

(block sysadm_gom
	(typeattribute sessbus_chat_client_subj_type_attribute)

	(blockinherit gom.subj_blk)

	(call sysadm_dconf.client_subj_type (subj))

	(optional sysadm_gom_optional_sysadm_gdocs
		(call sysadm_gdocs.sessbus_chat_client_subj_type (subj)))

	(optional sysadm_gom_optional_sysadm_goad
		(call sysadm_goad.sessbus_chat_client_subj_type (subj)))

	(optional sysadm_gom_optional_sysadm_gvfsd
		(call sysadm_gvfsd.sessbus_chat_client_subj_type (subj)))

	(optional sysadm_gom_optional_sysadm_gvfsdud2
		(call sysadm_gvfsdud2.sessbus_chat_client_subj_type (subj)))

	(optional sysadm_gom_optional_sysadm_gvfsdgoavm
		(call sysadm_gvfsdgoavm.sessbus_chat_client_subj_type (subj)))

	(optional sysadm_gom_optional_sysadm_sessbus
		(call sysadm_sessbus.daemon (subj gom.cmd_file)))

	(optional sysadm_gom_optional_sysadm_tracker
		(call sysadm_tracker.client_subj_type (subj)))

	(optional
		sysadm_gom_sessbus_chat_client_subj_type_optional_sysadm_sessbus
		(allow sessbus_chat_client_subj_type_attribute subj (dbus
			(send_msg)))
		(allow subj sessbus_chat_client_subj_type_attribute (dbus
			(send_msg)))))
