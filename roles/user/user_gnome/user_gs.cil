;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Macros
;

(in user_gs
	(macro sessbus_chat_client_subj_type ((type ARG1))
		(typeattributeset sessbus_chat_client_subj_type_attribute
			ARG1))

	(macro daemon_subj_type ((type ARG1))
		(typeattributeset daemon_subj_type_attribute ARG1))

	(macro forked_subj_type ((type ARG1))
		(typeattributeset forked_subj_type_attribute ARG1))

	(macro daemon ((type ARG1)(type ARG2))
		(call daemon_subj_type (ARG1))
		(call auto_subj_type_transition_pattern (subj ARG2 ARG1)))

	(macro stream_connect ((type ARG1))
		(call gs.spec_stream_connect (ARG1 subj))))

;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Policy
;

(block user_gs
	(typeattribute daemon_subj_type_attribute)
	(typeattribute forked_subj_type_attribute)
	(typeattribute sessbus_chat_client_subj_type_attribute)

	(blockinherit gs.subj_blk)

	(call user_caribou.send_signal (subj))

	(call user_dconf.client_subj_type (subj))

	(call user_gsquit.auto_subj_type_transition (subj))

	(optional user_gs_optional_user_atspi_sessbus_launch
		(call user_atspi_sessbus_launch.auto_subj_type_transition
			(subj))
		(call user_atspi_sessbus_launch.sessbus_chat_client_subj_type
			(subj)))

	(optional user_gs_optional_user_canberra
		(call user_canberra.auto_subj_type_transition (subj)))

	(optional user_gs_optional_user_gvfs
		(call user_gvfsd.sessbus_chat_client_subj_type (subj)))

	(optional user_gs_optional_user_orca
		(call user_orca.send_signal (subj)))

	(optional user_gs_optional_user_pactl
		(call user_pactl.auto_subj_type_transition (subj)))

	(optional user_gs_optional_user_sessbus
		(call user_sessbus.service_subj_type (subj)))

	(optional user_gs_sessbus_chat_client_subj_type_optional_user_sessbus
		(allow sessbus_chat_client_subj_type_attribute subj (dbus
			(send_msg)))
		(allow subj sessbus_chat_client_subj_type_attribute (dbus
			(send_msg))))

	(call forked_subj_type (daemon_subj_type_attribute))
	(call send_signal (daemon_subj_type_attribute))

	(call rw_inherited_unix_stream_sockets (forked_subj_type_attribute)))

(block user_gsquit
	(blockinherit gsquit.subj_blk))
