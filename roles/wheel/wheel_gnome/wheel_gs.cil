;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Macros
;

(in wheel_gs
	(macro sessbus_chat_client_subj_type ((type ARG1))
		(typeattributeset sessbus_chat_client_subj_type_attribute
			ARG1))

	(macro daemon_subj_type ((type ARG1))
		(typeattributeset daemon_subj_type_attribute ARG1))

	(macro forked_subj_type ((type ARG1))
		(typeattributeset forked_subj_type_attribute ARG1))

	(macro daemon ((type ARG1)(type ARG2))
		(call daemon_subj_type (ARG1))
		(call auto_subj_type_transition_pattern (subj ARG2 ARG1)))

	(macro stream_connect ((type ARG1))
		(call gs.spec_stream_connect (ARG1 subj)))

	(macro auto_subj_type_transition_gs_helper ((type ARG1))
		(call gs_helper.spec_auto_subj_type_transition (ARG1 subj))))

;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Policy
;

(block wheel_gs
	(typeattribute daemon_subj_type_attribute)
	(typeattribute forked_subj_type_attribute)
	(typeattribute sessbus_chat_client_subj_type_attribute)

	(blockinherit gs.subj_blk)

	(call wheel_caribou.send_signal (subj))

	(call wheel_dconf.client_subj_type (subj))

	(call wheel_gsd.send_signal (subj))

	(call wheel_gsquit.auto_subj_type_transition (subj))

	(optional wheel_gs_optional_wheel_atspi_sessbus_launch
		(call wheel_atspi_sessbus_launch.auto_subj_type_transition
			(subj))
		(call wheel_atspi_sessbus_launch.sessbus_chat_client_subj_type
			(subj)))

	(optional wheel_gs_optional_wheel_canberra
		(call wheel_canberra.auto_subj_type_transition (subj)))

	(optional wheel_gs_optional_wheel_gvfs
		(call wheel_gvfsd.sessbus_chat_client_subj_type (subj)))

	(optional wheel_gs_optional_wheel_orca
		(call wheel_orca.send_signal (subj)))

	(optional wheel_gs_optional_wheel_pactl
		(call wheel_pactl.auto_subj_type_transition (subj)))

	(optional wheel_gs_optional_wheel_sessbus
		(call wheel_sessbus.service_subj_type (subj)))

	(optional wheel_gs_sessbus_chat_client_subj_type_optional_wheel_sessbus
		(allow sessbus_chat_client_subj_type_attribute subj (dbus
			(send_msg)))
		(allow subj sessbus_chat_client_subj_type_attribute (dbus
			(send_msg))))

	(call forked_subj_type (daemon_subj_type_attribute))
	(call send_signal (daemon_subj_type_attribute))

	(call rw_inherited_unix_stream_sockets (forked_subj_type_attribute)))

(block wheel_gsquit
	(blockinherit gsquit.subj_blk))
